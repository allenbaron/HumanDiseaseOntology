# Return ID, label, and asserted superclasses of all non-obsolete classes
# NOTE: language tags are removed from labels

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT ?id ?label (GROUP_CONCAT(?subClassOf_str; SEPARATOR="|") AS ?subClassOf)
WHERE {
    ?class a owl:Class ;
        oboInOwl:id ?id ;
        rdfs:label ?label_lang ;
        rdfs:subClassOf ?superclass .
    ?superclass rdfs:label ?subClassOf_lang .

    FILTER (CONTAINS(STR(?class), "DOID"))
    FILTER NOT EXISTS { ?class owl:deprecated ?obsolete . }

    BIND(str(?label_lang) AS ?label)
    BIND(str(?subClassOf_lang) AS ?subClassOf_str)
}
GROUP BY ?id ?label
ORDER BY ?label
