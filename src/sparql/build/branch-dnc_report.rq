# Return a count of non-obsolete classes by branch
# MUST to be queried against doid-non-classified.owl

PREFIX rdfs: <http://www.w3.org/2000/01/rdf-schema#>
PREFIX owl: <http://www.w3.org/2002/07/owl#>
PREFIX obo: <http://purl.obolibrary.org/obo/>
PREFIX oboInOwl: <http://www.geneontology.org/formats/oboInOwl#>

SELECT ?branch (COUNT(?class) AS ?count)
WHERE {
    # specify branches
    ?branch_class a owl:Class ;
        rdfs:subClassOf obo:DOID_4 .

    ?class a owl:Class ;
        rdfs:subClassOf* ?branch_class .
    ?branch_class rdfs:label ?branch .

    FILTER(!isBlank(?class))
    FILTER NOT EXISTS { ?class owl:deprecated ?obsolete . }
}
GROUP BY ?branch
ORDER BY ?branch
